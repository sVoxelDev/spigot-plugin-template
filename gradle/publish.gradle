apply plugin: 'maven-publish'

def getBranch() {
    def process = 'git branch --show-current'.execute()
    process.waitFor()
    return process.text.trim()
}

def getHash() {
    def process = 'git rev-parse HEAD'.execute()
    process.waitFor()
    return process.text.trim()
}

jar {
    manifest {
        attributes (
                'Build-Jdk': "${System.properties['java.vendor']} ${System.properties['java.vm.version']}",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Git-Branch': getBranch(),
                'Git-Hash': getHash()
                )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'net.silthus'
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "${artifactory_contextUrl}/libs-release-local"
            def snapshotsRepoUrl = "$buildDir/repos/libs-snapshot-local"
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}
